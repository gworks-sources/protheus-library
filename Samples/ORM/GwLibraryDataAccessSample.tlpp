#include "TOTVS.ch"

using namespace Gworks.Library.Classes

namespace Gworks.Sample.Classes

User Function GwDataAccessSample() // Gworks.Sample.Classes.U_GwDataAccessSample

    Local cJobEmp := "01"
    Local cJobFil := "01"
    Local aTables := {"SA1","SC5","SC6"}

    Local oAttr
    Local oData
    Local oResult
    Local cTes := "" as character

    oAttr := GwKeyValue():New()
    oAttr:Add('AliasFrom', 'SC5')
    oAttr:Add('AliasJoin', 'SC6')
    oAttr:Add('IndexFrom', 'C5_FILIAL+C5_NUM')
    oAttr:Add('IndexJoinFK', 'C6_FILIAL+C6_NUM')
    oAttr:Add('IndexJoinPK', 'C6_FILIAL+C6_ITEM')

    RPCSetEnv(;
        /* cRpcEmp  */ cJobEmp,;
        /* cRpcFil  */ cJobFil,;
        /* cEnvUser */ ,;
        /* cEnvPass */ ,;
        /* cEnvMod  */ ,;
        /* cFunName */ ,;
        /* aTables  */ aTables)

        oData := GwDataAccess():New("SC5")
        oData:SelectArea()
        oData:SetOrder('C5_FILIAL+C5_NUM')
        oData:GoTop()
        if oData:Seek('491176')

            ConOut("Localizou o pedido...")

            if oData:SetRelation(oAttr)

                ConOut("Definiu um relacionamento...")

                if oData:RelationSearch('C6_QTDVEN',15)
                    ConOut("Localizou um item...")
                    oResult := oData:RelationGetSearchResult()
                endif

                ConOut('Retornando os campos C6_PRODUTO e C6_TES..')
                oResult := oData:RelationGetValues({'C6_PRODUTO','C6_TES'})

                ConOut('Retornando C6_TES do item 7 do pedido...')
                cTes := oResult:GetValueByKey('0107'):GetValueByKey('C6_TES')

            endif

        endif

    RpcClearEnv()

Return
