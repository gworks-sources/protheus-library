#include 'totvs.ch'
#include "msobject.ch"

#define CRLF chr(13)+chr(10)
#define CRLF2 CRLF+CRLF

using namespace Gworks.Library.Utils
using namespace Gworks.library.Functions

namespace Gworks.Library.Classes

Class GwError From ErrorClass

    Public Data cError as character default ""
    Public Data cSuggestion as character default ""
    Public Data cTitleError as character default ""
    Public Data cAutoGRLog as character default ""
    Public Data lError as logical default .F.
    Public Data lCanReplaced as logical default .T.

    Public Method New() Constructor // construtor com argumentos
    Public Method Create() Constructor // construtor vazio
    Public Method SetError() // define a mensagem de erro desejada
    Public Method GetErrorByName() // retorna a mensagem de erro conforme sua definição em: "ERROR", "SUGGESTION", "TITLEERROR"
    Public Method DefineWithError() // define que a instância da classe foi definida com erro
    Public Method HasError() // indica que a instância da classe GwError foi definida como erro
    Public Method ShowError() // exibe a mensagem de erro entre: "INFO", "STOP", "ALERT", "HELP"
    Public Method SetAutoGRLog() // define a mensagem para exibição via função MostraErro()
    Public Method SetAutoGRLogFromArray() // define a mensagem para exibição via função MostraErro() a partir de um array (somente para funções mvc)
    Public Method ShowAutoGRLog() // mostra a janela de erro da função MostraErro() conforme a mensagem definida para o
    Public Method SetCanReplaced() // flag auxiliar para definir se a mensagem de erro definida na instância da classe pode ou não ser substituída

    Protected Method SetHelp()

EndClass

Method New(cError,cSuggestion,cTitleError,lError,lShowError,cOption) Class GwError; :New()
    Default cError := ""
    Default cSuggestion := ""
    Default cTitleError := ""
    Default lError := .T.
    Default lShowError := .F.
    Self:SetError(cError,cSuggestion,cTitleError,lError)
    if lShowError
        Self:ShowError(cOption)
    endif
Return Self

Method Create() Class GwError; :New()
    Self:lError := .F.
    Self:lCanReplaced := .T.
Return Self

Method SetError(cError,cSuggestion,cTitleError,lError) Class GwError
    Default cError := ""
    Default cSuggestion := ""
    Default cTitleError := FunName()
    Default lError := .T.
    Self:cError := cError
    Self:cSuggestion := cSuggestion
    Self:cTitleError := cTitleError
    Self:lError := .T.
    Self:lCanReplaced := .T.
    Self:GenCode := 0
    Self:Description := cError
Return

Method GetErrorByName(cOption) Class GwError
    Local cRet := ""
    Default cOption := ERROR
    Do Case
        Case cOption == "ERROR" ; cRet := Self:cError
        Case cOption == "SUGGESTION" ; cRet := Self:cSuggestion
        Case cOption == "TITLEERROR" ; cRet := Sefl:cTitleError
    EndCase
Return cRet

Method HasError() Class GwError
Return Self:lError

Method ShowError(cOption,cTitle,lShowAutoGRLog,lDefineWithError,lUpdateErrorClass) Class GwError
    Local cShowError := ""
    Default cOption := "HELP"
    Default cTitle := ::cTitleError
    Default lShowAutoGRLog := .T.
    Default lDefineWithError := .T.
    Default lUpdateErrorClass := .T.
    If !Empty(::cSuggestion)
        cShowError += CRLF2
        cShowError += ::cSuggestion // TODO:revisar..
    EndIf
    Do Case
        Case cOption == "INFO"  ; FwAlertInfo(cShowError,cTitle)
        Case cOption == "STOP"  ; FwAlertError(cShowError,cTitle)
        Case cOption == "ALERT" ; FwAlertWarning(cShowError,cTitle)
        Case cOption == "HELP"  ; SetHelp(cTitle,::cError,::cSuggestion)
    EndCase
    If !Empty(::cAutoGRLog) .And. lShowAutoGRLog
        ::ShowAutoGRLog()
    EndIf
    If lDefineWithError
        ::DefineWithError()
    EndIf
    If lUpdateErrorClass
        ::GenCode := 0
        ::Description := cShowError
    EndIf
Return

Method SetAutoGRLog(cAutoGRLog) Class GwError
    Self:cAutoGRLog := cAutoGRLog
Return

Method SetAutoGRLogFromArray(aError,cClassOrigin) Class GwError
    Default cClassOrigin := "FwFormModel"
    Do Case
        Case Upper(cClassOrigin) == Upper("FwFormModel")
            Self:cAutoGRLog := "Detalhes:" + CRLF
            Self:cAutoGRLog += "Id do formulário de origem: " + '[ ' + cValToChar(aError[01]) + ' ]' + CRLF
            Self:cAutoGRLog += "Id do campo de origem: "      + '[ ' + cValToChar(aError[02]) + ' ]' + CRLF
            Self:cAutoGRLog += "Id do formulário de erro: "   + '[ ' + cValToChar(aError[03]) + ' ]' + CRLF
            Self:cAutoGRLog += "Id do campo de erro: "        + '[ ' + cValToChar(aError[04]) + ' ]' + CRLF
            Self:cAutoGRLog += "Id do erro: "                 + '[ ' + cValToChar(aError[05]) + ' ]' + CRLF
            Self:cAutoGRLog += "Mensagem do erro: "           + '[ ' + cValToChar(aError[06]) + ' ]' + CRLF
            Self:cAutoGRLog += "Mensagem da solução: "        + '[ ' + cValToChar(aError[07]) + ' ]' + CRLF
            Self:cAutoGRLog += "Valor atribuído: "            + '[ ' + cValToChar(aError[08]) + ' ]' + CRLF
            Self:cAutoGRLog += "Valor anterior: "             + '[ ' + cValToChar(aError[09]) + ' ]' + CRLF
    EndCase
Return

Method ShowAutoGRLog() Class GwError
    AutoGRLog(::cAutoGRLog)
    MostraErro()
Return

Method DefineWithError(lError) Class GwError
    Default lError := .T.
    Self:lError := lError
Return

Method SetCanReplaced(lReplaced) Class GwError
    Default lReplaced := .T.
    ::lCanReplaced := lReplaced
Return

Method SetHelp(cTit,cErro,cSolucao,oModel) Class GwError
    Default cTit     := ""
    Default cErro    := ""
    Default cSolucao := ""
    Default oModel   := nil
    If ValType(oModel) == "O" .And. oModel:ClassName() == "FWFORMMODEL"
        oModel:SetErrorMessage( nil,;
                                nil,;
                                nil,;
                                nil,;
                                iif(!Empty(cTit),cTit,"Modelo/operação inválidos"),;
                                iif(!Empty(cErro),cErro,"Não foi possível validar o modelo de dados e/ou a operação realizada."),;
                                iif(!Empty(cSolucao),cSolucao,"Favor entrar em contato com o suporte técnico." ) )
    Else
        Help(nil, nil, cTit, nil, cErro, 1, 0, nil, nil, nil, nil, nil, {cSolucao})
    EndIf
Return
